// This package is to check the public API (binary) compatibility of the latest
// published release with `algoliasearch-client-java` package.

import me.champeau.gradle.japicmp.JapicmpTask

plugins {
  id 'java-library'
  id 'me.champeau.gradle.japicmp' version '0.4.1'
}

repositories {
  maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
  mavenCentral()
}

def baselineConfig = configurations.register('baseline')
def latestConfig = configurations.register('latest')

dependencies {
  baseline('com.algolia:algoliasearch-client-java:4.+') {
    transitive = false
  }
  latest(projects.algoliasearchCore)
}

def japicmp = tasks.register('japicmp', JapicmpTask) {
  dependsOn tasks.findByName("jar")
  oldClasspath.setFrom(baselineConfig)
  newClasspath.setFrom(latestConfig)
  onlyBinaryIncompatibleModified.set(true)
  failOnModification.set(true)
  txtOutputFile.set(file("$buildDir/reports/japi.txt"))
  ignoreMissingClasses.set(true)
  includeSynthetic.set(true)
}

tasks.findByName('check').configure {
  dependsOn japicmp
}
